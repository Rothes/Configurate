pluginManagement {
    repositories {
        maven {
            name = "stellardriftReleases"
            url = "https://repo.stellardrift.ca/maven/internal/"
            mavenContent { releasesOnly() }
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '1.0.0'
}

dependencyResolutionManagement {
    repositoriesMode = RepositoriesMode.FAIL_ON_PROJECT_REPOS
    repositories {
        pluginManagement.repositories.each(it.&add)
        // Allow checkstyle only to be resolved from mavenLocal if set to a snapshot
        if (settings.hasProperty('checkstyleVersion') && checkstyleVersion.endsWith("-SNAPSHOT")) {
            mavenLocal {
                content {
                    includeGroup("com.puppycrawl.tools")
                }
            }
        }
    }
}

enableFeaturePreview "TYPESAFE_PROJECT_ACCESSORS"

if (JavaVersion.current() < JavaVersion.VERSION_17) {
    throw new GradleException("""\
Configurate requires at least Java 17 to build, but you are running ${JavaVersion.current()}.

Your JAVA_HOME is currently: ${System.getenv("JAVA_HOME")}
Is this correct?
""")
}

def prefix = "configurate"

rootProject.name = "$prefix-parent"

// core
["core", "tool", "bom", "examples"].each {
    include ":$it"
    // findProject(":$it")?.name = "$prefix-$it"
}

// formats
["gson", "hocon", "jackson", "xml", "yaml"].each {
    include ":format:$it"
    // findProject(":format:$it")?.name = "$prefix-$it"
}

// extras
["kotlin", "guice", "dfu2", "dfu3", "dfu4", "dfu7"].each {
    include ":extra:$it"
    findProject(":extra:$it")?.name = "extra-$it"
}

includeBuild 'vendor', {
  name = "configurate-vendor"
}
includeBuild 'build-logic', {
    name = "configurate-build-logic"
}

